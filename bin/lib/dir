#!/usr/bin/env fish

set config "$HOME/.config/dir/list"

# accept other lists via stdin
if test -p /dev/stdin
	set config "/dev/stdin"
end

cat $config | while read -l line
	if test -z $line
		continue
	end

	set firstChar (string sub -l1 $line)

	## Skip if comment detected
	if [ "$firstChar" = "#" ]
		continue
	end

	echo $line | read -d '|' name value

	set -a names $name
	set -a values $value
end

set choice (printf %s\n $names | fzf --height 40%)
set result $status

if test -n $choice
	# figure out which one they picked.
	# dumb solution for fish not having
	# native hash maps
	set idx 0
	for n in $names
		set idx (math "$idx + 1")
		if [ "$choice" = "$n" ]
			## emit string containing full chosen path
			if test -n "$values[$idx]"
				echo $values[$idx]
			else
				# support basic lists (no delimiters)
				echo $names[$idx]
			end
			## we are done here
			exit $result
		end
	end
end
